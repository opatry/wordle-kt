buildscript {
    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.2'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.7.0'
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
    }
}

subprojects {
    afterEvaluate { proj ->

        if (proj.hasProperty('android')) {
            android {
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }

                if (proj.hasProperty('kotlin')) {
                    kotlinOptions {
                        jvmTarget = JavaVersion.VERSION_1_8.toString()
                    }
                }

                testOptions {
                    unitTests.all {
                        // let JUnit report mark the job status in case of error
                        ignoreFailures = true
                        testLogging {
                            events "passed", "skipped", "failed"
                            exceptionFormat "short"

                            debug {
                                exceptionFormat "full"
                            }
                        }
                    }
                }
            }
        }

        if (proj.hasProperty('kotlin') && !proj.hasProperty('android')) {
            compileKotlin {
                kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
            }
            compileTestKotlin {
                kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
            }
        }

        if (proj.hasProperty('test')) {
            test {
                // let JUnit report mark the job status in case of error
                ignoreFailures = true
                testLogging {
                    events 'passed', 'skipped', 'failed'
                    exceptionFormat 'short'

                    debug {
                        exceptionFormat 'full'
                    }
                }
            }
        }
    }
}
